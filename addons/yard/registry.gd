@tool
@icon("res://addons/yard/editor_only/assets/yard.svg")
@warning_ignore_start("unused_private_class_variable")
class_name Registry
extends Resource
## A registry associating resources with stable, human-readable string IDs.
##
## [Registry] lets you reference resources by stable string IDs (e.g. [code]&"enemy_skeleton"[/code])
## instead of file paths, which can silently change when assets are moved.
## It provides a bidirectional map between string IDs and UIDs, helpers to resolve and load
## entries individually or in bulk (synchronously or via threaded loading).[br][br]
##
## It also offers an optional property index for querying entries (resources) by their
## properties at runtime, without loading them. Since the property index is baked into
## the registry at editor time, querying is fast.
##
## [codeblock]
## const ENEMIES: Registry = preload("res://data/enemy_registry.tres")
## const WEAPONS: Registry = preload("res://data/weapon_registry.tres")
##
## func _show_skeleton() -> void:
##     var skeleton: Enemy = ENEMIES.load_entry(&"skeleton")
##     %Sprite.texture = skeleton.creature_sprite
##
## func _get_legendary_weapons() -> Array[StringName]:
##     return WEAPONS.filter_by_value(&"rarity", Rarity.LEGENDARY)
## [/codeblock][br]
##
## Registries and their entries are read-only at runtime and must be managed through
## the dedicated editor tab :[br][br]
##
## [img width=1200]res://addons/yard/editor_only/assets/ui_example.png[/img]
## [br][br]
##
## [b]See Also:[/b][br][br]
##
## • [Resource] - [i]Base class for serializable objects.[/i][br]
## • [ResourceLoader] - [i]A singleton for loading resource files.[/i][br]

## Constant to be used with [annotation @GDScript.@export_custom] instead of a [enum PropertyHint] value.
## Enables a dropdown in the inspector for any [StringName], [String], [Array][lb]StringName[rb] or
## [Array][lb]String[rb] property, populated with the string IDs of a [Registry].
## [br][br]
## The hint string accepts up to three comma-separated values:
## [br] • [b]registry path[/b] (required): [code]res://[/code] or [code]uid://[/code] path to the registry
## [br] • [b]show_empty[/b] (optional, default [code]false[/code]): adds a [code]<empty>[/code] option mapping to an empty string
## [br] • [b]allow_duplicates[/b] (optional, default [code]true[/code]): allows the same ID to appear multiple times in an
## [Array][lb]StringName[rb] or
## [Array][lb]String[rb]
## [codeblock]
## @export_custom(Registry.PROPERTY_HINT_CUSTOM, "res://data/item_registry.tres") var item: StringName
## @export_custom(Registry.PROPERTY_HINT_CUSTOM, "res://data/item_registry.tres,true") var item_or_empty: StringName
## @export_custom(Registry.PROPERTY_HINT_CUSTOM, "res://data/item_registry.tres,true,false") var items: Array[StringName]
## [/codeblock]
const PROPERTY_HINT_CUSTOM: int = 1024

@export_storage var _registry_version: int = 0
@export_storage var _class_restriction: StringName = &""
@export_storage var _scan_directory: String = ""
@export_storage var _recursive_scan: bool = false
# Bidirectional map. Populated by RegistryIO in the editor, read-only at runtime.
@export_storage var _uids_to_string_ids: Dictionary[StringName, StringName]
@export_storage var _string_ids_to_uids: Dictionary[StringName, StringName]
# Baked property index: property -> value -> set of resources string IDs.
@export_storage var _property_index: Dictionary[StringName, Dictionary] = { }


func _init() -> void:
	if not Engine.is_editor_hint():
		_uids_to_string_ids.make_read_only()
		_string_ids_to_uids.make_read_only()


## Returns the number of entries in the registry. Empty registries always return [code]0[/code].
## See also [method Registry.is_empty].
func size() -> int:
	return _uids_to_string_ids.size()


## Returns [code]true[/code] if the registry contains no entries.
## See also [method Registry.size].
func is_empty() -> bool:
	return _uids_to_string_ids.is_empty()


## Returns [code]true[/code] if [param property] has been baked into the property index.[br][br]
##
## Use this to guard calls to [method Registry.filter_by_value],
## [method Registry.filter_by], and [method Registry.filter_by_values]
## when indexing of a given property is not guaranteed.
func is_property_indexed(property: StringName) -> bool:
	return _property_index.has(property)


## Returns [code]true[/code] if the given [param id] exists in the registry.[br][br]
##
## The [param id] may be either a string ID (for example, [code]&"enemy_skeleton"[/code])
## or a UID (for example, [code]&"uid://dqtv77mng5dyh"[/code]).
func has(id: StringName) -> bool:
	return get_uid(id) != &""


## Returns [code]true[/code] if the given UID is present in the registry.
##
## The [param uid] must start with [code]uid://[/code].
func has_uid(uid: StringName) -> bool:
	return _uids_to_string_ids.has(uid)


## Returns [code]true[/code] if the given string ID is present in the registry.
func has_string_id(string_id: StringName) -> bool:
	return _string_ids_to_uids.has(string_id)


## Returns an [Array] of all registered UIDs.
##
## Each entry is a [StringName] in the form [code]&"uid://..."[/code].
func get_all_uids() -> Array[StringName]:
	return _uids_to_string_ids.keys()


## Returns an [Array] of all registered string IDs.
func get_all_string_ids() -> Array[StringName]:
	return _string_ids_to_uids.keys()


## Resolves any identifier (string ID or UID) to its canonical UID form.[br][br]
##
## If [param id] is already a registered UID, it is returned unchanged.
## If [param id] is a registered string ID, returns the corresponding UID.
## Returns an empty [StringName] when [param id] cannot be resolved.
func get_uid(id: StringName) -> StringName:
	if id.is_empty():
		return &""

	if id.begins_with("uid://"):
		return id if _uids_to_string_ids.has(id) else &""

	var string_id := StringName(id)
	return _string_ids_to_uids.get(string_id, &"")


## Returns the string ID associated with the given UID.
##
## Returns an empty [StringName] if [param uid] is not found in the registry.
func get_string_id(uid: StringName) -> StringName:
	if _uids_to_string_ids.has(uid):
		return _uids_to_string_ids[uid]
	else:
		return &""


## Returns an [Array] of all properties that have been baked into the property index.[br][br]
##
## Each entry in the returned array is a [StringName] corresponding to a property key
## that can be queried using [method Registry.filter_by], [method Registry.filter_by_value],
## or [method Registry.filter_by_values].[br][br]
##
## Use this method to inspect which properties are available for fast lookup at runtime,
## without loading the underlying resources.
func get_indexed_properties() -> Array[StringName]:
	return _property_index.keys()


## Loads the resource associated with [param id] (string ID or UID) and returns it.
## Returns [code]null[/code] if the entry does not exist or cannot be loaded.[br][br]
##
## [param type_hint] and [param cache_mode] are passed down to
## [method ResourceLoader.load].
func load_entry(
		id: StringName,
		type_hint: String = "",
		cache_mode: ResourceLoader.CacheMode = ResourceLoader.CACHE_MODE_REUSE,
) -> Resource:
	var uid := get_uid(id)
	if uid == &"" or not ResourceLoader.exists(uid):
		return null
	else:
		return ResourceLoader.load(uid, type_hint, cache_mode)


## Loads all registered resources in a blocking manner. Returns a dictionary
## mapping string IDs to their loaded [Resource] instances.
## Missing or invalid entries are skipped.[br][br]
##
## [param type_hint] and [param cache_mode] are passed down to
## [method ResourceLoader.load].
func load_all_blocking(
		type_hint: String = "",
		cache_mode: ResourceLoader.CacheMode = ResourceLoader.CACHE_MODE_REUSE,
) -> Dictionary[StringName, Resource]:
	var dict: Dictionary[StringName, Resource] = { }

	for uid in get_all_uids():
		if not uid == &"" and ResourceLoader.exists(uid):
			dict[_uids_to_string_ids[uid]] = ResourceLoader.load(
				uid,
				type_hint,
				cache_mode,
			)

	return dict


## Requests threaded loading for all entries and returns a [Registry.RegistryLoadTracker].
## The returned tracker can be used to monitor progress, inspect statuses,
## and retrieve loaded resources as they become available.[br][br]
## See also [method ResourceLoader.load_threaded_request].
func load_all_threaded_request(
		type_hint: String = "",
		use_sub_threads: bool = false,
		cache_mode := ResourceLoader.CACHE_MODE_REUSE,
) -> RegistryLoadTracker:
	var tracker := RegistryLoadTracker.new()

	for string_id: StringName in get_all_string_ids():
		var uid := get_uid(string_id)
		tracker.__uids[string_id] = uid
		tracker.__resources[string_id] = null
		var err := ResourceLoader.load_threaded_request(uid, type_hint, use_sub_threads, cache_mode)
		if err == OK:
			tracker.__requested[string_id] = true
			tracker.__status[string_id] = ResourceLoader.THREAD_LOAD_IN_PROGRESS
		else:
			tracker.__requested[string_id] = false
			tracker.__status[string_id] = ResourceLoader.THREAD_LOAD_INVALID_RESOURCE

	return tracker


## Returns the string IDs of all entries whose [param property] satisfies [param predicate].[br][br]
##
## [param predicate] receives the property value and must return a [bool].
## Requires the property index to have been baked for [param property].
## Returns an empty array if the property is not indexed or no value matches the predicate.
## [codeblock]
## var high_level := weapon_registry.filter_by(&"level", func(v): return v >= 10)
## var rare_or_epic := weapon_registry.filter_by(&"rarity", func(v): return v in [Rarity.RARE, Rarity.EPIC])
## [/codeblock]
func filter_by(property: StringName, predicate: Callable) -> Array[StringName]:
	var result: Array[StringName] = []
	if not _property_index.has(property):
		return result
	for value: Variant in _property_index[property]:
		if predicate.call(value):
			for string_id: StringName in _property_index[property][value]:
				result.append(string_id)
	return result


## Returns the string IDs of all entries whose [param property] equals [param value].[br][br]
##
## Requires the property index to have been baked for [param property].
## Returns an empty array if the property is not indexed or no entry has that value.
## [codeblock]
## var legendaries := weapon_registry.filter_by_value(&"rarity", Rarity.LEGENDARY)
## [/codeblock]
func filter_by_value(property: StringName, value: Variant) -> Array[StringName]:
	var result: Array[StringName] = []
	if not _property_index.has(property):
		return result
	var value_map: Dictionary = _property_index[property]
	if not value_map.has(value):
		return result
	result.assign(value_map[value].keys())
	return result


## Returns the string IDs of all entries matching all [param criteria] (AND logic).[br][br]
##
## [param criteria] is a [Dictionary] mapping property names to their expected values.
## Requires the property index to have been baked for each property.
## Returns an empty array if any property is not indexed or if the intersection yields no results.
## [codeblock]
## var perfect_armors := armor_registry.filter_by_values({&"defense": 100, &"weight": 0})
## var legendary_swords := weapon_registry.filter_by_values({&"rarity": Rarity.LEGENDARY, &"type": "sword"})
## [/codeblock]
func filter_by_values(criteria: Dictionary[StringName, Variant]) -> Array[StringName]:
	var result: Array[StringName] = []
	var first := true
	for property: StringName in criteria:
		var matches := filter_by_value(property, criteria[property])
		if first:
			result = matches
			first = false
		else:
			# Intersect: keep only IDs present in both
			var matches_set: Dictionary[StringName, bool] = { }
			for id in matches:
				matches_set[id] = true
			var intersected: Array[StringName] = []
			for id in result:
				if matches_set.has(id):
					intersected.append(id)
			result = intersected
		if result.is_empty():
			return result
	return result


## Loading tracker used with [method Registry.load_all_threaded_request]
##
## Returned by [method Registry.load_all_threaded_request].
## Provides information about asynchronous resource loading.[br][br]
## All its [Dictionary] properties use resources String IDs as keys :[br][br]
##  - [member progress] is the overall load progress ([code]0.0[/code]–[code]1.0[/code]).[br]
##  - [member status] matches an entry string ID to its current [enum ResourceLoader.ThreadLoadStatus].[br]
##  - [member resources] holds loaded [Resource] objects as they become ready.[br]
##  - [member uids] matches an entry string ID to its UID.[br]
##  - [member requested] tells if the entry was successfully requested through [method ResourceLoader.load_threaded_request].[br][br]
##
## [b]Note:[/b] Accessors automatically poll and update internal loading states before returning.
class RegistryLoadTracker extends RefCounted:
	var progress: float:
		get:
			_poll()
			return __progress

	var uids: Dictionary[StringName, StringName]:
		get:
			return __uids.duplicate()

	var requested: Dictionary[StringName, bool]:
		get:
			return __requested.duplicate()

	var status: Dictionary[StringName, ResourceLoader.ThreadLoadStatus]:
		get:
			_poll()
			return __status.duplicate()

	var resources: Dictionary[StringName, Resource]:
		get:
			_poll()
			return __resources.duplicate()

	var __progress: float = 0.0
	var __uids: Dictionary[StringName, StringName]
	var __requested: Dictionary[StringName, bool]
	var __status: Dictionary[StringName, ResourceLoader.ThreadLoadStatus]
	var __resources: Dictionary[StringName, Resource]


	func _poll() -> void:
		var n_res_requested := 0
		var n_res_loaded := 0.0 # allow fractional loading progress
		for uid: String in __uids.values():
			var res_progress := []
			if not __requested[uid]:
				continue
			n_res_requested += 1
			__status[uid] = ResourceLoader.load_threaded_get_status(uid, res_progress)
			n_res_loaded += res_progress[0]
			if (
				__status[uid] == ResourceLoader.THREAD_LOAD_LOADED
				and __resources[uid] == null
			):
				__resources[uid] = ResourceLoader.load_threaded_get(uid)
		__progress = n_res_loaded / n_res_requested
